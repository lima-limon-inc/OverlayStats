# Copyright 1999-2022 Gentoo Authors
# Distributed under the terms of the GNU General Public License v2

EAPI="8"
ETYPE="sources"
K_WANT_GENPATCHES="base extras"
K_GENPATCHES_VER="1"
K_SECURITY_UNSUPPORTED="1"
K_NOSETEXTRAVERSION="1"

inherit kernel-2
detect_version
detect_arch

KEYWORDS="~amd64 ~x86"
HOMEPAGE="https://gitlab.freedesktop.org/agd5f/linux"
IUSE=""

DESCRIPTION="The AMD DRM FIXES Live Sources"

DATE=${PV#*p}

ZEN_URI="https://github.com/zen-kernel/zen-kernel/releases/download/v${PV%_*}-zen1/v${PV%_*}-zen1.patch.xz"
AMD_DRM_FIXES_URI="https://gitlab.freedesktop.org/agd5f/linux/-/archive/amd-drm-fixes-${PV%_*}-${DATE:0:4}-${DATE:4:2}-${DATE:6:2}/linux-amd-drm-fixes-${PV%_*}-${DATE:0:4}-${DATE:4:2}-${DATE:6:2}.tar.gz"
SRC_URI="${AMD_DRM_FIXES_URI} ${GENPATCHES_URI} ${ARCH_URI} ${ZEN_URI}"

UNIPATCH_LIST="${DISTDIR}/v${PV%_*}-zen1.patch.xz"
UNIPATCH_STRICTORDER="yes"

K_EXTRAEINFO="For more info on ${PN}, and for how to report problems, see: \
${HOMEPAGE}."

pkg_setup() {
	ewarn
	ewarn "${PN} is *not* supported by the Gentoo Kernel Project in any way."
	ewarn "If you need support, please contact the amd linux developers directly."
	ewarn "Do *not* open bugs in Gentoo's bugzilla unless you have issues with"
	ewarn "the ebuilds. Thank you."
	ewarn
	kernel-2_pkg_setup
}

pkg_postrm() {
	kernel-2_pkg_postrm
}
